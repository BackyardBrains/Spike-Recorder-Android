apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.backyardbrains"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 89
        versionName '1.8.3.1'

        externalNativeBuild {
            cmake {
                // Pass arguments to CMake
                arguments "-DANDROID_STL=c++_static"

                cppFlags "-frtti" // Enables RTTI support.
                cppFlags "-fexceptions" // Enables exception-handling support.
            }
        }

        // Enabling vector drawables
        vectorDrawables.useSupportLibrary = true

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments =
                    [eventBusIndex: 'com.backyardbrains.SpikeRecorderBusIndex', "room.schemaLocation": "$projectDir/schemas".
                        toString()]
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
//            ext.enableCrashlytics = false
        }
    }

    flavorDimensions "devProd"

    productFlavors {
        production {
            dimension 'devProd'
        }
        development {
            dimension 'devProd'
            minSdkVersion 21
            resConfigs "en", "xxhdpi,xhdpi"
            ndk.abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    if (project.hasProperty('devBuild')) {
        aaptOptions.cruncherEnabled = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    androidXCoreVersion = '1.0.2'
    androidXMediaVersion = '1.0.1'
    androidXLegacySupportV4Version = '1.0.0'
    constraintLayoutVersion = '1.1.3'
    roomVersion = '2.1.0'
    materialDesignVersion = '1.0.0'
    firebaseVersion = '17.0.1'
    butterknifeVersion = '10.1.0'
    materialDialogsVersion = '0.9.6.0'
    simpleRangeBarVersion = '0.1.1'
    crashlyticsVersion = '2.10.1'
    easyPermissionsVersion = '2.0.1'
    eventBusVersion = '3.1.1'
    eventBusAnnotationProcessorVersion = '3.0.1'
    essentialsVersion = '3.0.0-RC1'
    usbSerialVersion = '6.1.0'
    commonsMathVersion = '3.6.1'
    benchitVersion = 'v1.0.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // androidX
    implementation "androidx.core:core:$androidXCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidXCoreVersion"
    implementation "androidx.media:media:$androidXMediaVersion"
    implementation "androidx.legacy:legacy-support-v4:$androidXLegacySupportV4Version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"

    implementation platform('com.google.firebase:firebase-bom:28.0.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-core'

    // material design
    implementation "com.google.android.material:material:$materialDesignVersion"

    // debug db
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    // view binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // material dialogs
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"

    // range bar
    implementation "com.github.recklessracoon:SimpleRangeBar-Android:$simpleRangeBarVersion"

    // permissions
    implementation "pub.devrel:easypermissions:$easyPermissionsVersion"

    // event bus
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:$eventBusAnnotationProcessorVersion"

    // utils
    implementation "org.greenrobot:essentials:$essentialsVersion"

    // serial communication
    //    implementation "com.github.felHR85:UsbSerial:$usbSerialVersion"
    //    implementation project(path: ':usbserial', configuration: 'default')
    implementation(name: 'usbserial-6.1.0-release', ext: 'aar')
    // next 2 lines load dependencies that are used by UsbSerial module (aar doesn't transfer dependencies)
    implementation 'com.annimon:stream:1.2.1'
    implementation 'com.squareup.okio:okio:2.1.0'

    // signal filtering
    implementation "org.apache.commons:commons-math3:$commonsMathVersion"
    // benchmark
    implementation "com.github.T-Spoon:Benchit:$benchitVersion"
}